plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.portfolio'
version = '1.0.0'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    
    // Spring Boot Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    
    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    
    // Spring Mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    
    // H2 Database
    runtimeOnly 'com.h2database:h2'
    
    // MariaDB Driver
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    
    // PostgreSQL Driver (Railway용)
    runtimeOnly 'org.postgresql:postgresql'
    
    // Spring Boot Actuator (헬스체크용)
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    // JSON Processing
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    
    // Apache Commons Exec for Python execution
    implementation 'org.apache.commons:commons-exec:1.3'
    
    // Spring Boot DevTools (비활성화 - QAOA 실행 중 자동 재시작 방지)
    // developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    // Spring Boot Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// 프론트엔드 빌드 자동화
task buildFrontend(type: Exec) {
    group = 'build'
    description = 'Build frontend using npm'
    
    workingDir 'frontend'
    
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'npm', 'run', 'build'
    } else {
        commandLine 'npm', 'run', 'build'
    }
}

task copyFrontend(type: Copy) {
    group = 'build'
    description = 'Copy frontend build to static resources'
    
    dependsOn buildFrontend
    
    from 'frontend/dist'
    into 'src/main/resources/static'
}

// bootJar와 bootRun 실행 전에 프론트엔드 빌드
tasks.named('bootJar') {
    dependsOn copyFrontend
}

tasks.named('bootRun') {
    dependsOn copyFrontend
}

tasks.named('processResources') {
    dependsOn copyFrontend
}

// Azure 배포용 설정
jar {
    enabled = false
}

bootJar {
    archiveFileName = 'qfn-portfolio-optimizer.jar'
    mainClass = 'com.portfolio.optimizer.StockPortfolioOptimizerApplication'
}
